Syntax = 1*Rule
Rule = OptWhitespace RuleName OptWhitespace AssignmentOperator OptWhitespace Expression 1*SingleLineEnd
AssignmentOperator = "="
AssignmentOperator =/ "=/"
OptWhitespace = *" "
Expression = List 0*ExpressionEnd
ExpressionEnd = OptWhitespace "/" OptWhitespace List
SingleLineEnd = OptWhitespace *1Comment LF
Comment = ";" *(WSP / VCHAR)
List = Term 0*ListEnd
ListEnd = 1*" " Term
Term = RepeatableTerm / RepeatTerm
RepeatTerm = RepeatBody RepeatableTerm / "[" RepeatableTerm "]"
GroupTerm = "(" OptWhitespace Expression OptWhitespace ")"
RepeatBody = 0*1Number "*" 0*1Number / Number
RepeatableTerm = StringLiteral / BinaryLiteral / HexadecimalLiteral / DecimalLiteral / RuleName / GroupTerm
StringLiteral = *1("%s" / "%i") LiteralBody
LiteralBody = DQUOTE 0*Character DQUOTE
Digit = "0" / NonZeroDigit
Letter = %d65-90 / %d97-122
Character = Letter / Digit / Symbol
NonZeroDigit = %d49-57
Symbol = %d32-33 / %d35-39 / %d40-47 / %d58-64 / %d91-96 / %d123-126
RuleName = Letter 0*OneRuleEnd
OneRuleEnd = Letter / Digit / "-" Letter / "-" Digit
Number = Digit / NonZeroDigit 1*Digit
BinaryLiteral = "%b" BinaryBody 0*1BinaryTail
BinaryBody = 1*%d48-49
BinaryTail = "-" BinaryBody / 1*BinaryConcatUnit
BinaryConcatUnit = "." BinaryBody
HexadecimalLiteral = "%x" HexadecimalBody 0*1HexadecimalTail
HexadecimalBody = 1*HexadecimalBodyUnit
HexadecimalTail = "-" HexadecimalBody / 1*HexadecimalConcatUnit
HexadecimalBodyUnit = %d48-57 / %d65-70
HexadecimalConcatUnit = "." HexadecimalBody
DecimalLiteral = "%d" DecimalBody 0*1DecimalTail
DecimalBody = 1*%d48-57
DecimalConcatUnit = "." DecimalBody
DecimalTail = "-" DecimalBody / 1*DecimalConcatUnit
