<Syntax> ::= <Rule>{1,} <EOF>
<Rule> ::= <OptWhitespace> <RuleName> <OptWhitespace> <AssignmentOperator> <OptWhitespace> <Expression> <SingleLineEnd>{1,}
<AssignmentOperator> ::= "=" | "=/"
<OptWhitespace> ::= " "{0,}
<Expression> ::= <List> <ExpressionEnd>{0,}
<ExpressionEnd> ::= <OptWhitespace> "/" <OptWhitespace> <List>
<SingleLineEnd> ::= <OptWhitespace> <Comment>{0,1} <EOL>
<Comment> ::= ";" <CommentChar>{0,}
<CommentChar> ::= /33-126/ | " " | /9/
<List> ::= <Term> <ListEnd>{0,}
<ListEnd> ::= " "{1,} <Term>
<Term> ::= <RepeatableTerm> | <RepeatTerm>
<RepeatTerm> ::= <RepeatBody> <RepeatableTerm> | "[" <RepeatableTerm> "]"
<GroupTerm> ::= "(" <OptWhitespace> <Expression> <OptWhitespace> ")"
<RepeatBody> ::= <Number>{0,1} "*" <Number>{0,1} | <Number>
<RepeatableTerm> ::= <StringLiteral> | <BinaryLiteral> | <HexadecimalLiteral> | <DecimalLiteral> | <RuleName> | <GroupTerm>
<StringLiteral> ::= <LiteralBody> | "%s" <LiteralBody> | "%i" <LiteralBody>
<LiteralBody> ::= '"' <Character>{0,} '"'
<Digit> ::= "0" | <NonZeroDigit>
<Character> ::= <Letter> | <Digit> | <Symbol>
<Letter> ::= /65-90/ | /97-122/
<NonZeroDigit> ::= /49-57/
<Symbol> ::= /32-33/ | /35-39/ | /40-47/ | /58-64/ | /91-96/ | /123-126/
<RuleName> ::= <Letter> <OneRuleEnd>{0,}
<OneRuleEnd> ::= <Letter> | <Digit> | "-" <Letter> | "-" <Digit>
<Number> ::= <Digit> | <NonZeroDigit> <Digit>{1,}
<BinaryLiteral> ::= "%b" <BinaryBody> <BinaryTail>{0,1}
<BinaryBody> ::= /48-49/{1,}
<BinaryTail> ::= "-" <BinaryBody> | <BinaryConcatUnit>{1,}
<BinaryConcatUnit> ::= "." <BinaryBody>
<HexadecimalLiteral> ::= "%x" <HexadecimalBody> <HexadecimalTail>{0,1}
<HexadecimalBody> ::= <HexadecimalBodyUnit>{1,}
<HexadecimalTail> ::= "-" <HexadecimalBody> | <HexadecimalConcatUnit>{1,}
<HexadecimalBodyUnit> ::= /48-57/ | /65-70/
<HexadecimalConcatUnit> ::= "." <HexadecimalBody>
<DecimalLiteral> ::= "%d" <DecimalBody> <DecimalTail>{0,1}
<DecimalBody> ::= /48-57/{1,}
<DecimalConcatUnit> ::= "." <DecimalBody>
<DecimalTail> ::= "-" <DecimalBody> | <DecimalConcatUnit>{1,}
